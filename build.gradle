defaultTasks 'clean', 'check', 'jar'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'nexus-workflow'

version = '1.0.1'
project.group = 'org.javafunk'
archivesBaseName = "excel-parser"

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.adaptc.gradle:nexus-workflow:0.6'
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compile 'org.projectlombok:lombok:1.14.4'
    compile 'org.apache.poi:poi-ooxml:3.10-FINAL'

    testCompile 'org.testng:testng:6.8.1'
}

signing {
    sign configurations.archives
}

jar {
    manifest {
        attributes 'Implementation-Title': 'Excel Parser', 'Implementation-Version': version
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
    manifest {
        attributes 'Implementation-Title': 'Excel Parser', 'Implementation-Version': version
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
    manifest {
        attributes 'Implementation-Title': 'Excel Parser', 'Implementation-Version': version
    }
}

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
    repositories {
        flatDir {
            dirs 'repos'
        }

        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(
                        userName: project.property("oss-releases.username"),
                        password: project.property("oss-releases.password"))
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(
                        userName: project.property("oss-releases.username"),
                        password: project.property("oss-releases.password"))
            }

            pom.project {
                name 'Excel Parser'
                packaging 'jar'
                description 'Annotations for Excel document parsing.'
                url 'https://github.com/tobyclemson/excel-parser'

                scm {
                    connection 'scm:git:git://github.com/tobyclemson/excel-parser/'
                    developerConnection 'scm:git:git://github.com/tobyclemson/excel-parser/'
                    url 'https://github.com/tobyclemson/excel-parser'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'nvenky'
                        name 'Venkatesh Nannan'
                        email 'nvenky@gmail.com'
                    }
                }
            }
        }
    }
}
